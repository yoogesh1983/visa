package com.yoogesh.rules
 
import com.yoogesh.visa.model.Passport;
import com.yoogesh.visa.validation.Validation;
import java.time.LocalDate;

rule "Valid passport"
	dialect "mvel"
	when
		$passport: Passport( this.validation == Validation.UNKNOWN )
	then
		System.out.println( $passport + " is without validation info, consider OK for now" );
		modify($passport) {
		  setValidation( Validation.PASSED )
		}
end

rule "Expired passport"
	dialect "mvel"
	when
		$passport: Passport( this.isExpired()  )
	then
		System.out.println( $passport + " is INVALID because it is expired" );
		modify($passport) {
		  setValidation( Validation.FAILED ),
		  setCause( "passport is expired" )
		}
end

rule "Full passport"
	dialect "mvel"
	when
		$passport: Passport( this.unusedVisaPages == 0   )
	then
		System.out.println( $passport + " is INVALID because it has no space for visa" );
		modify($passport) {
		  setValidation( Validation.FAILED ),
		  setCause( "passport has no space for visa" )
		}
end